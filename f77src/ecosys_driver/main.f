      PROGRAM main
C     THIS SUBROUTINE READS THE RUNSCRIPT AND ENTERS FILENAMES INTO 
C     DATA ARRAYS FOR USE IN 'READS' AND 'READQ' CALLED FROM �SOIL�. 
C     WHEN FINISHED THIS SUBROUTINE CALLS 'SOIL' WHICH IS THE MAIN 
C     SUBROUTINE FROM WHICH ALL OTHERS ARE CALLED
C
      include "parameters.h"
      include "filec.h"
      include "files.h"
      include "blkc.h"
      DIMENSION NA(250),ND(250)
      CHARACTER*16 DATA(30),DATAC(30,250,250),DATAP(JP,JY,JX)
     2,DATAM(JP,JY,JX),DATAX(JP),DATAY(JP),DATAZ(JP,JY,JX)
     3,OUTS(10),OUTP(10),OUTFILS(10,JY,JX),OUTFILP(10,JP,JY,JX)
      CHARACTER*3 CHOICE(102,20)
      CHARACTER*8 CDATE
      CHARACTER*80 BUF,PREFIX
      CALL GETCWD(BUF)
C
C     IDENTIFY OPERATING SYSTEM: DOS OR UNIX. IF UNIX,
C     THE PREFIX "./" CAN BE REPLACED BY "../" SO THAT ALL INPUT
C     FILES ARE CALLED FROM THE DIRECTORY ABOVE THAT IN WHICH
C     THE RUNSCRIPT IS EXECUTING. THIS FEATURE IS USED WHEN DIFFERENT
C     RUNS SHARE A COMMON POOL OF INPUT FILES
C
      IF((.NOT.(BUF(1:1).EQ.'/'.OR.BUF(1:1).EQ.'~'))
     2.AND.BUF(2:2).EQ.':')THEN
      CALL GETARG(1,BUF)
      OPEN(5,FILE=BUF,STATUS='OLD')
      CALL GETARG(2,BUF)
      CALL CHDIR(BUF)
C     make output directory
      outdir='.\\outputs\\'
      ELSE
      PREFIX='./'
C     make output directory
      buf='./'
      outdir='./outputs/'
      ENDIF
      call system('mkdir -p '//trim(outdir))
C
C     READ INPUT FILES
C
10    FORMAT(A16)
      IGO=0
C
C     LANDSCAPE GRID CELL DOMAIN FROM NW (NHW,NVN) TO SE (NHE,NVS).
C     ALL GRID CELLS IN THIS DOMAIN MUST BE REPRESENTED IN INPUT FILES
C     FOR TOPOGRAPHY, SOIL AND PLANT MANAGEMENT 
C
      READ(5,*)NHW,NVN,NHE,NVS
C
C     SITE FILE IDENTIFYING KEY SITE AND BOUNDARY CONDITIONS 
C     COMMON TO ALL GRID CELLS
C
      READ(5,10)DATA(1)

C     TOPOGRAPHY FILE IDENTIFYING TOPOGRAPHIC AND SOIL CONDITIONS
C     FOR EACH GRID CELL
C
      READ(5,10)DATA(2)
C
C     NUMBER OF SCENARIOS AND THE NUMBER OF TIMES THESE 
C     SCENARIOS ARE TO BE EXECUTED IN THE MODEL RUN.
C     SCENARIOS MAY CONSIST OF ONE YEAR (EG PLANTING) OR SEVERAL 
C     YEARS (EG ROTATION, CONTINUOUS PERENNIAL VEGETATION) THAT
C     MAY BE REPEATED DURING THE MODEL RUN.
C
100   READ(5,*)NAX,NDX
      IF(NAX.EQ.0.AND.NDX.EQ.0)GO TO 1000
C
C     NUMBER OF SCENES IN THE NEXT SCENARIO OF THE MODEL RUN
C     AND THE NUMBER OF TIMES THESE SCENES ARE TO BE EXECUTED.
C     SCENES MAY CONSIST OF A PART OF A YEAR OR A FULL YEAR. 
C
      DO 105 NEX=1,NAX
      READ(5,*)NAY,NDY
      NA(NEX)=NAY
      ND(NEX)=NDY
C
C     FOR EACH SCENE IN THIS SCENARIO:
C
      DO 110 NE=1,NA(NEX)
C
C     WEATHER FILE IDENTIFIES SURFACE BOUNDARY CONDITIONS
C     FOR SHORTWAVE RADIATION, LONGWAVE RADIATION (OPTIONAL),
C     AIR TEMPERATURE, HUMIDITY, WINDSPEEN AND PRECIPITATION
C
      READ(5,10,END=1000)DATAC(3,NE,NEX)
C
C     WEATHER OPTIONS ENABLE PRESCRIBED MANIPULATIONS OF DATA
C     IN THE WEATHER FILE 
C
      READ(5,10)DATAC(4,NE,NEX)
C
C     LAND MANAGEMENT FILE IDENTIFIES FILES CONTAINING INFORMATION
C     ABOUT DISTURBANCES, FERTILIZERS AND OTHER AMENDMENTS,
C     AND IRRIGATION IN EACH GRID CELL
C
      READ(5,10)DATAC(9,NE,NEX)
C
C     BIOME MANAGEMENT FILE IDENTIFOES PLANT FUNCTIONAL TYPTES
C     AND THEIR MANAGEMENT IN EACH GRID CELL. 
C
      READ(5,10)DATAC(10,NE,NEX)
C
C     OUTPUT DATA EDITORS ENABLING SELECTION OF VARIABLES TO BE
C     INCLUDED IN OUTPUT FILES. 
C
      DO 115 N=21,30
      READ(5,10)DATAC(N,NE,NEX)
115   CONTINUE
110   CONTINUE
105   CONTINUE
C
C     RUN ALL GRID CELLS IN THIS SCENARIO
C
      DO 120 NTX=1,NDX
      DO 120 NEX=1,NAX
      DO 120 NT=1,ND(NEX)
      DO 120 NE=1,NA(NEX)
      CALL SOIL(NA,ND,NT,NE,NAX,NDX,NTX,NEX,NHW,NHE,NVN,NVS)
      IGO=IGO+1
120   CONTINUE
C
C     SCENARIO COMPLETED, START NEXT SCENARIO
C
      GO TO 100
1000  STOP
      END

